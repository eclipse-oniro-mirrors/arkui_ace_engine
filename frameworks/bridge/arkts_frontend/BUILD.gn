# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/arkui/ace_engine/ace_config.gni")
import("./link_abc.gni")

group("arkts_frontend_native_package") {
  deps = [ "$ace_root/frameworks/bridge/arkts_frontend/koala_projects/arkoala-arkts/arkui-ohos/src/ani/native:ace_ani_native_package" ]
}
group("arkoala_ui2abc") {
  deps = [ "koala_projects/arkoala-arkts:arkoala" ]
}

link_abc_files("arkoala_abc") {
  targets = [
    "koala_projects/incremental/common:common_abc",
    "koala_projects/incremental/compat:compat_abc",
    "koala_projects/incremental/runtime:runtime_abc",
    "koala_projects/interop:interop-arkts_abc",
    "koala_projects/arkoala-arkts:components_abc"
  ]

  output_name = "arkoala"
}

ohos_prebuilt_etc("arkoala_abc_prebuild") {
  source = "$target_out_dir/arkoala.abc"
  deps = [ ":arkoala_abc" ]
  module_install_dir = "framework"
  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
}

ohos_source_set("arkts_frontend") {
  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
  configs = [ "$ace_root:ace_config" ]
  deps = [
    ":ArkoalaNative_ark",
    "arkoala:arkoala_prebuild",
  ]

  if (build_ohos_sdk) {
    deps -= [
      ":ArkoalaNative_ark",
      "arkoala:arkoala_prebuild",
    ]
  }

  sources = [
    "ani_context_module.cpp",
    "arkts_ani_utils.cpp",
    "arkts_frontend.cpp",
    "entry/arkts_entry_loader.cpp",
  ]
  external_deps = [
    "hilog:libhilog",
    "runtime_core:ani",
  ]
}

ohos_shared_library("ArkoalaNative_ark") {
  include_dirs = [
    "koala_projects/arkoala/framework/native/src",
    "koala_projects/arkoala/framework/native/src/generated",
    "koala_projects/interop/src",
    "koala_projects/interop/src/cpp",
    "koala_projects/interop/src/cpp/ani",
    "koala_projects/interop/src/cpp/ets",
    "koala_projects/interop/src/cpp/types",
    "koala_projects/interop/src/cpp/ohos",
  ]

  defines = [
    "KOALA_OHOS",
    "__MUSL__",
    "KOALA_ETS_NAPI",
    "DKOALA_USE_PANDA_VM",
    "KOALA_ANI",
  ]

  sources = [
    "koala_projects/arkoala/framework/native/src/events.cc",
    "koala_projects/arkoala/framework/native/src/generated/bridge_custom.cc",
    "koala_projects/arkoala/framework/native/src/generated/bridge_generated.cc",
    "koala_projects/arkoala/framework/native/src/generated/callback_deserialize_call.cc",
    "koala_projects/arkoala/framework/native/src/generated/callback_managed_caller.cc",
    "koala_projects/arkoala/framework/native/src/generated/library.cc",
    "koala_projects/arkoala/framework/native/src/vsync.cc",
  ]

  # interop source
  sources += [
    "koala_projects/interop/src/cpp/callback-resource.cc",
    "koala_projects/interop/src/cpp/common-interop.cc",
    "koala_projects/interop/src/cpp/ani/convertors-ani.cc",
    "koala_projects/interop/src/cpp/interop-logging.cc",
    "koala_projects/interop/src/cpp/types/signatures.cc",
  ]

  deps = [
    "koala_projects/arkoala-arkts/arkui-ohos/src/ani/native:arkoala_native_ani",
  ]

  #configs = [ ":arkoala_native_config" ]

  external_deps = [
    "hilog:hilog_ndk",
    "hilog:libhilog",
    "runtime_core:ani",
  ]

  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
}

if (use_linux) {
  loader_include_dirs = [
    "koala_projects/arkoala/framework/native/src",
    "koala_projects/arkoala/framework/native/src/generated",
    "koala_projects/arkoala/node_modules/node-api-headers/include",
    "koala_projects/interop/src",
    "koala_projects/interop/src/cpp",
    "koala_projects/interop/src/cpp/ets",
    "koala_projects/interop/src/cpp/types",
    "koala_projects/interop/src/cpp/ohos",
    "koala_projects/interop/src/cpp/napi",
  ]

  ohos_shared_library("ArkoalaLoader") {
    include_dirs = loader_include_dirs

    defines = [
      "INTEROP_LIBRARY_NAME=ArkoalaNative_linux_x64",
      "KOALA_LINUX",
      "KOALA_NAPI",
      "KOALA_USE_NODE_VM",
      "USE_DUMMY_IMPL",
      "LIBACE_LINUX_X64",
    ]

    sources = [
      "koala_projects/arkoala/framework/native/src/generated/bridge_custom.cc",
      "koala_projects/arkoala/framework/native/src/generated/library.cc",
      "koala_projects/arkoala/framework/native/src/vsync.cc",
    ]

    # interop source
    sources += [
      "koala_projects/interop/src/cpp/common-interop.cc",
      "koala_projects/interop/src/cpp/interop-logging.cc",
      "koala_projects/interop/src/cpp/napi/convertors-napi.cc",
    ]

    external_deps = [
      "hilog:hilog_ndk",
      "napi:ace_napi",
    ]

    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
  }

  ohos_shared_library("vmloader") {
    include_dirs = loader_include_dirs + [ "koala_projects/interop/src/cpp/ani" ]

    defines = [
      "KOALA_LINUX",
      "KOALA_ANI",
      "KOALA_ETS_NAPI",
    ]

    sources = [ "koala_projects/interop/src/cpp/vmloader.cc" ]

    external_deps = [
      "hilog:hilog_ndk",
      "napi:ace_napi",
    ]

    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
  }

  native_include_dirs = [
    "koala_projects/arkoala/framework/native/src",
    "koala_projects/arkoala/framework/native/src/generated",
    "koala_projects/arkoala/node_modules/node-api-headers/include",
    "koala_projects/interop/src/cpp",
    "koala_projects/interop/src/cpp/napi",
    "koala_projects/interop/src/cpp/types",
  ]

  native_sources = [
    "koala_projects/arkoala/framework/native/src/events.cc",
    "koala_projects/arkoala/framework/native/src/generated/bridge_custom.cc",
    "koala_projects/arkoala/framework/native/src/generated/bridge_generated.cc",
    "koala_projects/arkoala/framework/native/src/generated/callback_deserialize_call.cc",
    "koala_projects/arkoala/framework/native/src/generated/callback_managed_caller.cc",
    "koala_projects/arkoala/framework/native/src/generated/library.cc",
    "koala_projects/arkoala/framework/native/src/vsync.cc",
    "koala_projects/interop/src/cpp/callback-resource.cc",
    "koala_projects/interop/src/cpp/common-interop.cc",
    "koala_projects/interop/src/cpp/interop-logging.cc",
    "koala_projects/interop/src/cpp/types/signatures.cc",
  ]

  native_defines = [
    "INTEROP_LIBRARY_NAME=ArkoalaNative_linux_x64",
    "KOALA_LINUX",
    "KOALA_USE_PANDA_VM",
    "KOALA_FOREIGN_NAPI",
    "LIBACE_LINUX_X64",
  ]

  ohos_shared_library("ArkoalaNative_ani") {
    include_dirs = native_include_dirs + [ "koala_projects/interop/src/cpp/ani" ]
    defines = native_defines + [ "KOALA_ANI" ]
    sources = native_sources + [ "koala_projects/interop/src/cpp/ani/convertors-ani.cc" ]

    external_deps = [
      "hilog:hilog_ndk",
      "napi:ace_napi",
    ]

    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
  }

  ohos_shared_library("ArkoalaNative_ets") {
    include_dirs = native_include_dirs + [ "koala_projects/interop/src/cpp/ets" ]
    defines = native_defines + [ "KOALA_ETS_NAPI" ]
    sources = native_sources + [ "koala_projects/interop/src/cpp/ets/convertors-ets.cc" ]

    external_deps = [
      "hilog:hilog_ndk",
      "napi:ace_napi",
    ]

    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
  }
}
