# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("./link_abc.gni")

group("arkts_frontend_native_package") {
  deps = [
    ":arkts_frontend",
    "koala_projects/arkoala-arkts/arkui-ohos/src/ani/native:ace_ani_native_package"
  ]
}
group("arkoala_ui2abc") {
  deps = [ "koala_projects/arkoala-arkts:arkoala" ]
}

link_abc_files("arkoala_abc") {
  targets = [
    "koala_projects/incremental/common:common_abc",
    "koala_projects/incremental/compat:compat_abc",
    "koala_projects/incremental/runtime:runtime_abc",
    "koala_projects/interop:interop-arkts_abc",
    "koala_projects/arkoala-arkts:components_abc",
  ]

  output_name = "arkoala"
}

ohos_prebuilt_etc("arkoala_abc_prebuild") {
  source = "$target_out_dir/arkoala.abc"
  deps = [ ":arkoala_abc" ]
  module_install_dir = "framework"
  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
}

template("arkts_frontend") {
  forward_variables_from(invoker, "*")

  ohos_shared_library("arkts_frontend") {
    defines += invoker.defines
    configs = [ "$ace_root:ace_config" ]
    deps = [
      ":ArkoalaNative_ark",
      "$ace_root/build:libace_compatible"
    ]

    sources = [
      "ani_context_module.cpp",
      "arkts_frontend.cpp",
      "arkts_plugin_frontend.cpp",
      "entry/arkts_entry_loader.cpp",
    ]

    sources += [ "$ace_root/adapter/ohos/entrance/dynamic_component/eaworker_task_wrapper_impl.cpp" ]
    external_deps = [
      "hilog:libhilog",
      "runtime_core:ani",
      "runtime_core:ani_helpers",
      "runtime_core:libarkruntime",
    ]

    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
  }
}

template("arkts_frontend_loader") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
    defines += invoker.defines
    configs = [ "$ace_root:ace_config" ]

    sources = [
      "$ace_root/adapter/ohos/entrance/dynamic_component/arkts_dynamic_renderer_loader.cpp",
      "arkts_frontend_loader.cpp",
    ]
  }
}

foreach(item, ace_platforms) {
  arkts_frontend_loader("arkts_frontend_" + item.name) {
    defines = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }
  }

  if (item.name == "ohos") {
    arkts_frontend("arkts_frontend") {
      defines = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }

      if (defined(config.defines)) {
        defines = config.defines
      }
    }
  }
}

config("arkoala_native_config") {
  include_dirs = [
    "koala_projects/arkoala-arkts/framework/native/src",
    "koala_projects/arkoala-arkts/framework/native/src/generated",
    "koala_projects/interop/src",
    "koala_projects/interop/src/cpp",
    "koala_projects/interop/src/cpp/ani",
    "koala_projects/interop/src/cpp/ets",
    "koala_projects/interop/src/cpp/types",
    "koala_projects/interop/src/cpp/ohos",
    "$ace_root/interfaces/inner_api/ace_kit/include"
  ]
}

ohos_shared_library("ArkoalaNative_ark") {
  defines = [
    "KOALA_OHOS",
    "__MUSL__",
    "KOALA_ANI",
    "KOALA_USE_PANDA_VM",
  ]

  sources = [
    # "koala_projects/arkoala-arkts/framework/native/src/resource_color_helper.cc",
    # "koala_projects/arkoala-arkts/framework/native/src/events.cc",
    "koala_projects/arkoala-arkts/framework/native/src/generated/bridge_custom.cc",
    "koala_projects/arkoala-arkts/framework/native/src/generated/bridge_generated.cc",
    "koala_projects/arkoala-arkts/framework/native/src/generated/callback_deserialize_call.cc",
    "koala_projects/arkoala-arkts/framework/native/src/generated/callback_managed_caller.cc",
    "koala_projects/arkoala-arkts/framework/native/src/generated/library.cc",
    "koala_projects/arkoala-arkts/framework/native/src/DeserializerBase.cc",
    "koala_projects/arkoala-arkts/framework/native/src/vsync.cc",
  ]

  # interop source
  sources += [
    "koala_projects/interop/src/cpp/ani/convertors-ani.cc",
    "koala_projects/interop/src/cpp/callback-resource.cc",
    "koala_projects/interop/src/cpp/common-interop.cc",
    "koala_projects/interop/src/cpp/interop-logging.cc",
    "koala_projects/interop/src/cpp/types/signatures.cc",
  ]

  configs = [ ":arkoala_native_config" ]

  external_deps = [
    "hilog:hilog_ndk",
    "hilog:libhilog",
    "runtime_core:ani",
  ]


  if (is_arkui_x) {
    deps += [ "//third_party/bounds_checking_function:libsec_static" ]
  } else {
    external_deps += [ "bounds_checking_function:libsec_static" ]
  }

  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
}
