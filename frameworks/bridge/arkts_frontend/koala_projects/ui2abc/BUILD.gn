# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")

node_version = "v16.20.2"
host_arch = "${host_os}-${host_cpu}"

libarkts_root = "./libarkts"

action("fast_arktsc_build") {
  script = "../gn/command/npm_util.py"

  outputs = [ "$target_out_dir/fast_arktsc_build" ]
  deps = [ "../arkoala-arkts:ui2abc_process" ]
  args = [
    "--project-path",
    rebase_path("./fast-arktsc"),
    "--node-path",
    rebase_path(
        "//prebuilts/build-tools/common/nodejs/node-${node_version}-${host_arch}/bin"),
    "--npm-args",
    "run",
    "compile",
  ]
}

action("ui2abc_install") {
  script = "../gn/command/npm_util.py"
  outputs = [ "$target_out_dir/ui2abc_install" ]
  args = [
    "--project-path",
    rebase_path("."),
    "--node-path",
    rebase_path(
        "//prebuilts/build-tools/common/nodejs/node-${node_version}-${host_arch}/bin"),
    "--install",
  ]
}

action("ui2abc_build") {
  script = "../gn/command/npm_util.py"

  outputs = [ "$target_out_dir/MemoTransformer.js" ]

  deps = [
    ":fast_arktsc_build",
    "$libarkts_root:es2panda",
    "../arkoala-arkts:ui2abc_process",
  ]

  args = [
    "--project-path",
    rebase_path("."),
    "--node-path",
    rebase_path(
        "//prebuilts/build-tools/common/nodejs/node-${node_version}-${host_arch}/bin"),
    "--npm-args",
    "run",
    "build:gn",
  ]
}

group("build_es2panda_lib") {
  external_deps = [ ets2abc_build_deps ]
  external_deps += [ static_linker_build_deps ]
  external_deps += [ "ets_frontend:libes2panda_public(${host_toolchain})" ]
}

action("ui2abc_panda_sdk") {
  script = "gn/command/gen_sdk.py"

  outputs = [ "$target_out_dir/panda-sdk" ]

  deps = [ ":build_es2panda_lib" ]

  args = [
    "--config",
    rebase_path("gn/sdk_config.json"),
    "--src",
    rebase_path("//"),
    "--dist",
    rebase_path("./build/sdk"),
    "--out-root",
    rebase_path(root_out_dir),
  ]
}

group("ui2abc") {
  deps = [ ":ui2abc_build" ]
}
