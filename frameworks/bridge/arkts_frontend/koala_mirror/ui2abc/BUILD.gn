# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//foundation/arkui/ace_engine/frameworks/bridge/arkts_frontend/koala_mirror/gn/npm_util.gni")

koala_root = ".."

node_version = "v16.20.2"
host_arch = "${host_os}-${host_cpu}"

libarkts_root = "./libarkts"
build_root = "//build"

npm_cmd("fast_arktsc_build") {
    outputs = [
        "$target_out_dir/fast_arktsc_build"
    ]
    deps = [
        ":ui2abc_install_all(${host_toolchain})"
    ]
    project_path = rebase_path("./fast-arktsc")
    run_tasks = [ "compile" ]
}

if (current_toolchain == host_toolchain) {
    npm_install("ui2abc_install") {
        outputs = [
            "$target_out_dir/ui2abc_install"
        ]
        project_path = rebase_path(".")
    }
}

npm_cmd("ui2abc_build") {
    outputs = [
        "$target_out_dir/MemoTransformer.js"
    ]

    deps = [
        ":fast_arktsc_build",
        "$libarkts_root:es2panda"
    ]

    project_path = rebase_path(".")
    run_tasks = [ "build:plugins" ]
}

action("ui2abc_panda_sdk") {
    script = "gn/command/gen_sdk.py"

    outputs = [
        "$target_out_dir/panda-sdk"
    ]

    external_deps = [ets2abc_build_deps]
    external_deps += [static_linker_build_deps]
    external_deps += [ "ets_frontend:libes2panda_public(${host_toolchain})" ]

    args = [
        "--config", rebase_path("gn/sdk_config.json"),
        "--src", rebase_path("//"),
        "--dist", rebase_path("./build/sdk"),
        "--out-root", rebase_path(root_out_dir),
        "--current-os", current_os,
        "--current-cpu", current_cpu
    ]
}

group("ui2abc_install_all") {
    deps = [
        ":ui2abc_install(${host_toolchain})",
        "$koala_root/incremental:incremental_install(${host_toolchain})",
        "$koala_root/interop:interop_install(${host_toolchain})"
    ]
}

group("ui2abc") {
    deps = [
        ":ui2abc_build"
    ]
}