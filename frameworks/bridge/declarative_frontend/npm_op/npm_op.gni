# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/arkui/ace_engine/ace_config.gni")

template("npm_install_op") {
  assert(defined(invoker.project_path), "project_path is required")
  # parse parameters
  project_path = invoker.project_path
  stamp_file = "$target_gen_dir/${target_name}_npm_install.stamp"

  action(target_name) {
    script = "$ace_root/frameworks/bridge/declarative_frontend/npm_op/npm_install.py"
    inputs = [ "${project_path}/package.json" ]
    outputs = [ stamp_file ]
    args = [
      rebase_path("${project_path}"),
      rebase_path(stamp_file),
    ]
    if (!defined(deps)) {
      deps = []
    }
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
  }
}

template("npm_run_op") {
  assert(defined(invoker.cmd), "cmd is required")
  assert(defined(invoker.project_path), "project_path is required")
  assert(defined(invoker.watched_files), "watched_files is required")
  assert(defined(invoker.out_dir), "out_dir is required")
  assert(defined(invoker.out_file), "out_file is required")

  # parse parameters
  cmd = invoker.cmd
  watched_files = invoker.watched_files
  project_path = invoker.project_path
  out_dir = invoker.out_dir
  out_file = invoker.out_file

  action(target_name) {
    script = "$ace_root/frameworks/bridge/declarative_frontend/npm_op/npm_run.py"
    inputs = watched_files
    outputs = [ "${target_gen_dir}/${out_file}" ]
    args = [
      cmd,
      rebase_path("${project_path}"),
      out_dir,
      out_file,
      rebase_path("${target_gen_dir}"),
    ]
    if (!defined(deps)) {
      deps = []
    }
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
  }
}