# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/arkui/ace_engine/test/unittest/ace_unittest.gni")
import("../capi_common.gni")

ace_unittest("capi_all_modifiers_test") {
  type = "new"
  module_output = "capi"
  sources = [
    # place here your UT source code
  ]
  sources += [
    "generated/test_fixtures.cpp",
    "generated/test_fixtures_enums.cpp",
  ]
  extra_deps = []
  extra_deps += [
    ":capi_deps",
    "${ace_root}/frameworks/core/interfaces/native:ace_core_interfaces_native_node_linux_unittest",
    "../stubs:capi-ut-stubs",
  ]
}

config("capi_config") {
  include_dirs = [ "." ]
  if (capi_asan) {
    cflags_cc = [ "-fsanitize=address" ]
    ldflags = [ "-fsanitize=address" ]
  }
  if (capi_theme_debug) {
    defines = [ "CAPI_BACKTRACE" ]
  }
}

source_set("capi_deps") {
  testonly = true
  public_configs = [ ":capi_config" ]
  sources = [ "modifiers_test_utils.cpp" ]
  configs += [ "$ace_root/test/unittest:ace_unittest_config" ]
  deps = [ "${ace_root}/frameworks/core/interfaces/native:ace_core_interfaces_native_node_linux_unittest" ]
  external_deps = [
    "googletest:gmock",
    "googletest:gtest_main",
  ]
  if (capi_theme_debug) {
    sources += [ "unwind_utils.cpp" ]
    external_deps += [ "libunwind:libunwind" ]
    deps += [
      ":copy_libunwind",
      ":copy_libunwind_unstripped",
    ]
  }
}

if (capi_theme_debug) {
  copy("copy_libunwind") {
    testonly = true
    sources = [ root_out_dir + "/thirdparty/libunwind/libunwind.so" ]
    outputs = [ root_out_dir + "/tests/unittest/ace_engine/capi/libunwind.so" ]
    external_deps = [ "libunwind:libunwind" ]
  }
  copy("copy_libunwind_unstripped") {
    testonly = true
    sources = [ root_out_dir +
                "/lib.unstripped/clang_x64/thirdparty/libunwind/libunwind.so" ]
    outputs = [ root_out_dir + "/exe.unstripped/clang_x64/tests/unittest/ace_engine/capi/libunwind.so" ]
    external_deps = [ "libunwind:libunwind" ]
  }
}

group("capi_modifiers_unittest") {
  testonly = true
  deps = [
    ":capi_all_modifiers_test",
    #"generated:capi_generated_modifiers_unittest",
  ]
}
